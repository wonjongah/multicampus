![img](https://blogfiles.pstatic.net/MjAyMDA5MjFfMTM1/MDAxNjAwNjY0MDk2NDEx._83olGFwXL7rLfwnWC2OaBDyu0F4Ls0ZiCWvb1uR2cgg.p5oM4zd0wLpz2rRMKEdU4liHGG-0Mp1nBrVt3UDIp6Eg.JPEG.til_t/P20200916_163830715_1FE123B9-C55C-423F-8C6D-D63E79E61562.jpg)


 

초음파 센서 -> 초음파를 이용하여 벽이나 사물의 거리를 측정하는 센서



VCC - 전원

Trig - 초음파를 발생시키라고 명령시키는 단자 (출력) output

Echo - 초음파 수신 (입력) input

GND - 접지



![img](https://blogfiles.pstatic.net/MjAyMDA5MjFfMTEw/MDAxNjAwNjY0MzUwMTc1.Ti0gEmVlK6hCxxAek1oi9nyYwRGj1g-SQRGoyRo4KuUg.f1pyWzdR9LkxZ9cfO2joQMwBo5pclcy4GwrFxrLYAZkg.PNG.til_t/%EC%A0%9C%EB%AA%A9_%EC%97%86%EC%9D%8C.png)



 

평상시에 에코가 로우로 있다가 트리거가 보낸 하이가 벽에 부딪혀 돌아와 하이로 되는 시간을 체크하는 것

시간차이를 측정하여 거리 계산

t2 - t1 => 보내고 돌아올 때까지 거리(실제 시간) -> 이 실제시간(t)를 2로 나누어야 거리를 잴 수 있다

초음파의 속도 = 음속

340m/s

1초 : 340m = t초 : x 거리

왕복시간! -> x

x/2를 해야 거리를 잴 수 있다



delayMicroseconds() -> ms 이용

아두이노 보드를 일정 시간 멈추는 함수

delayMicroseconds(멈출 시간);



pulseIn(에코핀번호, 전압)

전압 -> high(low에서 high가 될 때)

반환값 -> 1초 기다렸는데도 전압이 바뀌지 않으면 0을 반환

에코가 평소에 로우로 있다가 펄스인을 호출하면 호출한 시점부터 로우에서 하이가 되는 시점을 리턴해줌 -> 비례식에서 t초를 리턴한다고 생각

트리거가 보낸 것을 언제 받았냐

t2 - t1을 해주는 함수





![img](https://blogfiles.pstatic.net/MjAyMDA5MjFfMTY3/MDAxNjAwNjY0MDk4ODU1.lEHZmn60x5Hu2vzM4HfOGV9s0gjTgB-seg9Vfb9nXREg.LzrHB6LD8UVYHZ8wn_LTbJlsUP5_KoN_Uc93NOirP54g.JPEG.til_t/P20200916_164157647_E38316B7-4EBA-4488-8A36-F01FAB993E27.jpg)









**VCC는 전원에,GND를 power의 GND에, 디지털 2번 핀에 에코를, 디지털 3번 핀에 트리거를 연결한다**







< ultra_ex01.ino >





int echoPin = 2;



int triggerPin = 3;





void setup(){



 Serial.begin(9600);



 pinMode(echoPin, INPUT);



 pinMode(triggerPin, OUTPUT);



}





void loop(){



 // trigger 핀으로 10ms의 펄스를 발생



 digitalWrite(triggerPin, HIGH);



 delayMicroseconds(10);



 digitalWrite(triggerPin, LOW);





 // echo 핀 입력으로부터 거리를 cm 단위로 계산



 int distance = pulseIn(echoPin, HIGH) / 58;



 



 Serial.println("Distance(cm) = " + String(distance));





 delay(1000);



}







![img](https://blogfiles.pstatic.net/MjAyMDA5MjFfMjQ4/MDAxNjAwNjY1Njg3Mzg3.nf5kZIhXuo5083up-olq76aDHvIir_oLZ0X4Imu_3Csg.MinW1S5tzb2E95o7QiLqS5d1cGLQeuxMbADPMQcsztMg.PNG.til_t/ddd.PNG)


![img](https://blogfiles.pstatic.net/MjAyMDA5MjFfNTEg/MDAxNjAwNjY1NzI1ODcx.8v5J1WLVog2JqMNI4Pe7O3BvlxqEs8BprmTba4Dx_e8g.zVimjrjuUEVE1kXnXNFUfyw_-FCLb3LklmjC0yl17TYg.JPEG.til_t/P20200916_170539533_6D18BF4B-9493-4E81-96C4-853E6A4DE4C9.jpg)




< ultra_ex02.ino >





\#include <MiniCom.h>

\#include <Ultra.h>

\#include <Led.h>

\#include <Servo.h>

\#include "Pulse.h"



MiniCom com;

Led led(8);

Ultra ultra(2, 3);

Servo servo;

Pulse pulse(100, 500);



int delayTimes[] = {

  50, 100, 200, 500, 1000

}; // ms, 다섯 단계로 나누어 간격을 나눔



void pulseCallback(int value){

  // led 제어

  led.set(value);

  // value가 트루면 켜지고 폴스면 꺼진다

}



// int echoPin = 2;

// int triggerPin = 3;



void checkDistance(){

  // trigger 핀으로 10ms의 펄스를 발생

  // digitalWrite(triggerPin, HIGH);

  // delayMicroseconds(10);

  // digitalWrite(triggerPin, LOW);



  // // echo 핀 입력으로부터 거리를 cm 단위로 계산

  // int distance = pulseIn(echoPin, HIGH) / 58;



  int distance = ultra.read();

  com.print(0, "distance", distance);

  if(distance < 10){

​    // led.on(); /

​    // pulse의 offdelay를 distance를 고려하여 조정

​    // int offDelay = map(distance, 0, 9, 50, 1000); // 거리에 따라 딜레이타임을 균등하게 나눔

​    int offDelay = map(distance, 0, 9, 0, 4); // 0~4는 delayTimes의 개수

​    // 균등하게 말고 가까이 있을 때는 짧게, 멀리 있을 때는 delayTime을 크게

​    // offDelay는 delayTimes의 인덱스

​    pulse.setDelay(10, delayTimes[offDelay]); // high의 값을 10으로 고정, low의 값을 distance에 따라 조정

​    // 근접하면 offdelay 작아지고, 멀어지면 offdelay 커질 것

​    servo.write(90);

​    if(!pulse.getState()){ // 처음 10cm 이하로 들어왔을 때

​      pulse.play();

​    }

  } else {

​    if(pulse.getState()){

​      pulse.stop();

​    }

​    // led.off();

​    servo.write(0);

  }

}

void setup(){

  com.init();

  servo.attach(9);

  servo.write(0);

  pulse.setCallback(pulseCallback);

  com.setInterval(1000, checkDistance);

}



void loop(){

  com.run();

  pulse.run(); // false or true에 따라서 운영하거나 안 하거나

}









< Pulse.h >







\#pragma once



\#include <Arduino.h>



typedef void (*pulse_callback_t)(int); // pulse_callback_t이 타입이 된 것



class Pulse{

  protected:

​    int onDelay; // HIGH 시간

​    int offDelay; // LOW 시간



​    int value; // 현재 상태값 (H/L)

​    unsigned long oldTime; // 최근의 상태 변경 시점 기록



​    bool state; // 항상 pulse운영하지 않고, 일정 값 이상이면 운영

​    pulse_callback_t callback;



  public:

​    Pulse(int onDelay, int offDelay);



​    void setDelay(int onDelay, int offDelay);

​    void run();

​    int read() {return value;}



​    bool getState() {return state;} 

​    void play();

​    void stop();



​    void setCallback(pulse_callback_t callback){this->callback = callback;}



};







< Pulse.cpp >







\#include "Pulse.h"



Pulse::Pulse(int onDelay, int offDelay)

: onDelay(onDelay), offDelay(offDelay){



  value = HIGH;

  state = false;

  callback = NULL; // null 초기화

  oldTime = millis(); // 현재 시간  

}



void Pulse::setDelay(int onDelay, int offDelay){

  this->onDelay = onDelay;

  this->offDelay = offDelay;

} // distance 바뀔 때 온은 그대로 두고 오프를 조정하면 된다



void Pulse::play(){ // 운영 시작

  state = true;

  value = HIGH;

  oldTime = millis();

}



void Pulse::stop(){ // 운영 정지

  state = false;

  value = LOW;

}



void Pulse::run(){

  if(!state) return; // state가 false이면 그냥 리턴해버림, 운영 안 함



  unsigned long currentTime = millis();

  unsigned long diff = currentTime - oldTime;

  

  long interval = value ? onDelay : offDelay; // value가 트루면 on 폴스면 off



  if(diff >= interval) // 시간이 다 되었으면

  {

​    // 엣지트리거, 상승, 하강 (값이 바뀔 때)

​    oldTime = currentTime;

​    value = !value; 

​    if(callback!=NULL){

​      callback(value);

​    }

  }

} // 시간 측정해서 하이가 되야 할 지 로우가 되어야 할 지 


![img](https://blogfiles.pstatic.net/MjAyMDA5MjFfMTAz/MDAxNjAwNjY4MDM2NjQ4.oIids9I1ABOyjyESBLREKC4OLnDANOsJrCI6kyQDRYgg.r3QvyZnjJkQD_BhYeczsb6Joioz0sikqw551zyRwi1Qg.PNG.til_t/3333.png)

![img](https://blogfiles.pstatic.net/MjAyMDA5MjFfMTU5/MDAxNjAwNjY0MTAwNzAx.3oSMkbydxET-iILGI9JKLhddQS6iUxTcH3nxL9SeBLUg.qaAZ26Fwvw4be0z-7nXsMKKrbOS_OKzlRJpy0g81d1Ag.JPEG.til_t/P20200916_174839762_0336635D-C03A-468E-BA3D-56B894A6D883.jpg)







**아까의 상태에 power의 -가 연결된 쪽에 330옴짜리 저항을 연결하고 저항의 나머지 다리에 led의 -부분을 연결한다**

**액티브 부저의 -부분을 초음파 센서의 GND 부분에 연결, 액티브 부저의 +부분을 led의 +에 연결한다**

**led의 + 부분과 디지털 핀의 8번을 연결한다**

**서보모터의 +는 빵판의 +, -는 빵판의 -에 나머지 핀은 디지털 9번 핀에 연결한다
**


![img](https://blogfiles.pstatic.net/MjAyMDA5MjFfMjg4/MDAxNjAwNjY0MTAyNjI3._ALJdFEfORQviemD9JiqctV4H7DI1CwpQpk0o8JxliUg.l3LeCIx4PVBNkV8zind5oWefcvSYUld18V7eOBdhkRkg.JPEG.til_t/P20200917_093746939_4DFD1EAD-EB6A-4DAF-82FD-E4FBB4B1919F.jpg)

![img](https://blogfiles.pstatic.net/MjAyMDA5MjFfMjg1/MDAxNjAwNjY0MTA1NjQ1.9mm2T8Ql4k4WnJkgB6A6vBF7TcgdWRmGV0-hz4yhrlAg.tl3pSZddnb9TLORGhk3uSTQEPG71-t1yrSvRLe3_7aMg.JPEG.til_t/P20200917_094514722_3E02F5FA-1097-4C9D-AA6E-765D1081DCCD.jpg)



![동영상은 수정하실 수 없습니다.](https://ssl.pstatic.net/static.se2/static/img/alter_video.png)