
![img](https://blogfiles.pstatic.net/MjAyMDA5MTdfOTcg/MDAxNjAwMzQ4MjgwNzI2.YX3SU4CxIF2LoqDzTLoFy-nR4V3bfV1lDQjl5-nTgB4g.EZAQ5oFltBY9oBw0GKMEVd1jIA-Gkl_yJ2rcekzf7V0g.JPEG.til_t/IMG_1099.jpg)



![img](https://blogfiles.pstatic.net/MjAyMDA5MTdfODQg/MDAxNjAwMzQ4MjgwMTQ3.WldoGTFc55nsMoSGfBoQhL-dq88KRHKiE3WoVwE6ty8g.1QQoGCWLwTiBUFTqD4b-wSGSSqBItH3XOMPHlxJEMFYg.JPEG.til_t/IMG_1100.jpg)







**서보모터**

**
**

\- 미리 정해진 범위 내에서 모터의 축을 세부 조정하도록 제한된 모터

\- 도어락, 주차차단기, 카메라 방향 조정 등에 쓰임

\- 제어를 위한 특별한 회로 없어서 바로 연결 가능

\- 0~180도 범위로 회전

\- 주황선 : 디지털 핀, 빨강선 : 전원 VCC, 갈색선 : GND

\- 신호가 HIGH로 설정된 시간의 양 -> 서보의 각도



![img](https://blogfiles.pstatic.net/MjAyMDA5MTdfMTA1/MDAxNjAwMzQ4MjgxMTUy.ak2M_u8mQnVuCd4Ea8LHMHZRA3btyCDcx0MKw-DB-C0g.CViul9GnDilYmyAEh_u_UYGYhDXbkk2K6ViZJdAU6_4g.JPEG.til_t/IMG_0959.jpg) 







**갈색선은 GND, 빨강선은 VCC, 주황선은 디지털 9번 핀에 연결한다**





**Servo 클래스 객체**

**Servo.h**

**servomotor.attach(핀번호); -> 서보 모터의 데이터 선 연결 핀 설정**

**servomotor.write(각도); -> 0~179 서보 모터에 전달할 각도값 설정**





< servo_ex01.ino >





\#include <Servo.h>



Servo servoMotor;

const int servoMotorPin = 9;

int angle;

void setup() {

  servoMotor.attach(servoMotorPin);

  servoMotor.write(0);  // 초기값 설정

  delay(1000);

}

void loop() {

  for (angle = 0; angle <= 179; angle++) {  // 각도를 180까지 돌리고

​    servoMotor.write(angle);

​    delay(10);

  }

  delay(100);  



  for (angle = 179; angle >= 0; angle--) {  // 180이었던 각도를 다시 0도로 돌리기

​    servoMotor.write(angle);

​    delay(10);

  }

  delay(100);

}











![img](https://blogfiles.pstatic.net/MjAyMDA5MTdfMTI4/MDAxNjAwMzQ5ODgxNDMz.Bal7If8DyJai977L8Bu0YkZUZvLxeo3gcasy16Dwl70g.ljOzl96ZfkgtCPl6YWkDB7MCcK3QOjJOToTiZT_1YE0g.JPEG.til_t/IMG_1101.jpg) 





**
**

**서보모터의 주황을 디지털 9번에, 빨강을 빵판의 +에 갈색을 빵판의 -에 연결한다**

**power의 VCC와 빵판의 +를 연결하고 power의 GND를 빵판의 -와 연결한다**

**가변저항을 가운데는 A0에, 뒤집어서 봤을 땐 왼쪽이 빵판의 -와 오른쪽이 빵판의 +와 연결되도록 한다**







< servo_ex02.ino > -> 가변저항을 이용해서 모터 각도 조절하기







\#include <LiquidCrystal_I2C.h>

\#include <Servo.h>

\#include <AnalogSensor.h>

\#include <SimpleTimer.h>



AnalogSensor poten(A0, 0, 179);

LiquidCrystal_I2C lcd(0x27, 16, 2);

Servo servoMotor;

SimpleTimer timer;



const int servoPin = 9;



void motorControl(){

  char buf[17];



  int angle = poten.read();

  servoMotor.write(angle);



  sprintf(buf, "angle : %3d", angle);

  lcd.setCursor(0,0);

  lcd.print(buf);

}



void setup(){

  lcd.init();

  lcd.backlight();

  lcd.clear();



  servoMotor.attach(servoPin);

  servoMotor.write(poten.read());

  timer.setInterval(50, motorControl);

  Serial.begin(9600);

}



void loop(){

  timer.run();

}







< AnalogSensor.h >







\#pragma once



\#include <Arduino.h>



class AnalogSensor{

  protected:

  int pin;

  int toMin;

  int toMax;



  public:

  AnalogSensor(int pin, int toMin=0, int toMax=255);

  void setRange(int toMin, int toMax);

  int read();

};







< AnalogSensor.cpp >







\#include "AnalogSensor.h"



AnalogSensor::AnalogSensor(int pin, int toMin, int toMax)

:pin(pin), toMin(toMin), toMax(toMax){



}



void AnalogSensor::setRange(int toMin, int toMax){

  this->toMin = toMin;

  this->toMax = toMax;

}



int AnalogSensor::read(){

  int value = analogRead(pin);

  return map(value, 0, 1023, toMin, toMax);

}







![img](https://ssl.pstatic.net/static.se2/static/img/alter_video.png) 







![img](https://blogfiles.pstatic.net/MjAyMDA5MTdfNDMg/MDAxNjAwMzUwMjg1MjE0.m_8umiXDQTuuHuJbnEkxR_B4UyrpM8kxigKyivkmKj4g.pOqSbNqrprbBwXPCXW77fE0jlgI2r1RPB7qBjGpQfjAg.JPEG.til_t/IMG_0963.jpg)




**조이스틱**

 x, y 축으로 가변저항 + 버튼

R5(레지스터, 10k옴짜리 저항을 달 수 있게 해놓음) -> 이 스위치를 이용할 때 input_pullup을 써야 함

VRx -> 아날로그 핀에 (가변저항이니까)

VRy -> 아날로그 핀에 

SW -> 디지털핀에




![img](https://blogfiles.pstatic.net/MjAyMDA5MTdfMjUz/MDAxNjAwMzUwMjg1NTY4.-KQP2cykmOrktVw3Z3G13DvfbMJoSxhmuatuJep-lcAg.Z6i5-ocxsP0ismEe7wMP-5JLlEcdo4yuipg7ugGGSiEg.JPEG.til_t/IMG_0964.jpg)

![img](https://blogfiles.pstatic.net/MjAyMDA5MTdfMzgg/MDAxNjAwMzUwMjg2MDAy.I4WdnVGEyy3F7gy_TvMju99B9PBncoxuYf2u3a9Qnhcg.6eMkc8n-SaFHaQ8xQaXSjVJKC1tEASweJQEKVeRHlEAg.JPEG.til_t/IMG_0966.jpg)







R5 -> 10K 옴





![img](https://blogfiles.pstatic.net/MjAyMDA5MTdfMTQw/MDAxNjAwMzUwMjg2NTE4.hh7mnOxIYRLW8kgxpV-Z7XIXK8XRailsdSTaiWG-DBog.NMsPvGUrY83PNfBDkch22AVe2lL0uo09Ag8VqbMa9dEg.JPEG.til_t/IMG_0967.jpg) 







**GND는 power의 GND에, +5V는 power의 5V에, VRx는 A0에, VRy는 A1에, SW는 디지털핀 2번에 연결한다**





< joystick_ex01.ino > -> 조이스틱의 x, y의 값과 버튼이 눌린 것도 체크(눌렸으면 1, 디폴트 0)







\#include <LiquidCrystal_I2C.h>

\#include <SimpleTimer.h>

\#include <Servo.h>

\#include <AnalogSensor.h>

\#include <Button.h>



AnalogSensor jX(A0, -100, 100);

AnalogSensor jY(A1, -100, 100);

Button btn(2);



LiquidCrystal_I2C lcd(0x27, 16, 2);



SimpleTimer timer;



void readJoystick(){

  char buf[17];



  int x = jX.read();

  int y = jY.read();

  int z = btn.read();

  sprintf(buf, "x : %4d, y : %4d", x, y);

  lcd.setCursor(0,0);

  lcd.print(buf);



  sprintf(buf, "z : %d", z);

  lcd.setCursor(0,1);

  lcd.print(buf);

}



void setup() {

  lcd.init();

  lcd.backlight();

  lcd.clear();



  timer.setInterval(50, readJoystick);

  Serial.begin(9600);

}

void loop() {

timer.run();

}







< Button.h >



\#pragma once



typedef void (*button_callback_t)();



\#include <Arduino.h>



class Button{

  protected:

​    int pin;

​    bool state_previous;

​    bool state_current;

​    // void (*callback)();

​    button_callback_t callback;

  public:

​    Button(int pin);

​    int read();

​    void setCallback(button_callback_t callback);

​    int check();

};





< Button.cpp >





\#include "Button.h"





Button::Button(int pin): pin(pin) {

  pinMode(pin, INPUT_PULLUP);

  state_previous = true;

  callback = NULL;

}



int Button::read() {

  return !digitalRead(pin);

  // 풀업이라 반전

}



void Button::setCallback(void( * callback)()) {

  this->callback = callback;

}



int Button::check() {

  state_current = digitalRead(pin);

  if (!state_current) { // 누른 경우

​    if (state_previous == true) {

​      state_previous = false;

​      //버튼을 누른 시점에서 해야 할 작업

​      // work();

​      if (callback != NULL) {

​        callback();

​      }

​    }

​    delay(5);

  } else {

​    state_previous = true;

  }

}





![img](https://blogfiles.pstatic.net/MjAyMDA5MTdfMTAx/MDAxNjAwMzUyOTYyNjA4.zIN094E5Ky6pyhZq_UHO3geIV-tr6Fb76B7CYnhX1MYg.wrJ11a7OFPBf5VF4tGsZmWJG_K_ztQnPJw-ZyDm5hFcg.JPEG.til_t/IMG_1103.jpg)

![img](https://blogfiles.pstatic.net/MjAyMDA5MTdfMjc2/MDAxNjAwMzUyOTYzMjMz.vn64jSWJLbyP1BHQBoB31ECZzssEP30RBMKqKpy6cD8g.8V88SnQ3J4hDo6rm78NofDYIlwCFBmYqB74jUsAg0Jwg.JPEG.til_t/IMG_1104.jpg)











< joystick_ex02.ino > -> 조이스틱으로 x, y 값 측정과 동시에 클릭시(동시에 led 켜짐) 서보모터 조정하기









\#include <Joystick.h>

\#include <LiquidCrystal_I2C.h>

\#include <SimpleTimer.h>

\#include <Servo.h>

\#include <Led.h>



Led led(13);

const int SERVO_PIN = 4;

Servo servo;

SimpleTimer timer;

JoyStick joy(A0, A1, 3);

bool mode = true; // true : 주행모드, false : 카메라 방향 모드

LiquidCrystal_I2C lcd(0x27, 16, 2);



// joystick 값 읽고 출력하기

void readJoystick(){

  joystick_value_t value = joy.read();

  char buf[17];



  if(mode){ // 주행모드

​    sprintf(buf, "X:%4d/Y:%4d", value.x, value.y);

​    lcd.setCursor(0,0);

​    lcd.print(buf);

  } else { // 카메라 방향 모드

​    servo.write(value.x);

​    sprintf(buf, "Angle: %3d", value.x);

​    lcd.setCursor(0,1);

​    lcd.print(buf);

  }

}

// joystick 운영 모드 변겅

void changeMode(){

  mode = !mode;



  if(mode){ // 주행모드

​    joy.setRangeX(-255, 255);

​    led.off();

  } else { // 카메라 방향 모드

​    joy.setRangeX(0, 180);

​    led.on();

  }

}



void setup(){

  lcd.init();

  lcd.backlight();

  lcd.clear();

  servo.attach(SERVO_PIN);

  // DC 모터 : 속도조정은 pwm이용 (0~255), 전진, 후진 : +, -

  joy.setRangeX(-255, 255); 

  joy.setRangeY(-255, 255);

  joy.setCallback(changeMode); // 클릭시 모드가 바뀌면서 값의 범위를

  // 0~180



  led.off();



  timer.setInterval(50, readJoystick);

}



void loop(){

  timer.run();

  joy.check();

}






< Joystick.h >





배열은 대입문 동작 X, 배열명 자체가 배열의 첫번째를 가리키는 상수이기 때문에

클래스를 만들자!, 클래스는 왜 대입이 되나?

JoyStick a, b;

a = b; // 복사 생성자 때문에

컴파일러가 복사생성자를 만든다, 복사 생성자는 얕은 복사

동적 할당이라면 깊은 복사를 쓸 텐데 우리는 정적할당을 쓰고 있으니 깊은 복사가 필요 없다

우리가 배열을 안 쓰고 할 수 있는 일은 클래스

하지만 형태가 부담스럽다

class JoystickValue{

public:

  int x;

  int y;

  int z;

  

  생성자();

};

그래서 C때부터 사용하는 형태가 있다 struct

struct

{

  int x;

  int y;

  int z;

} value; // 복합 데이터 표현하는 방법





\#pragma once



\#include <Arduino.h>

\#include <AnalogSensor.h>

\#include <Button.h>



struct joystick_value_t

{

  int x;

  int y;

  int z;

}; // 복합 데이터 표현하는 방법



// class JoystickValue{

// public:

//   int x;

//   int y;

//   int z;

  

//   생성자();

// };



class JoyStick{

  protected: // private는 상속이 안 된다

  AnalogSensor jX;

  AnalogSensor jY;

  Button btn;



  public:

  JoyStick(int x, int y, int z);

  // Joystick(Joystick &other); // 복사 생성자 매개변수로 자기자신과 다른 녀석



  int readX();

  int readY();

  void setRangeX(int toMin, int toMax);

  void setRangeY(int toMin, int toMax);



  int readZ(); // 버튼의 상태 읽기

  void setCallback(button_callback_t callback);

  void check();



  joystick_value_t read(); // 한번 호출에 값 세 개 다 얻고 싶다

  // 파이썬에서는 리스트, 튜플 이용



};





< Joystick.cpp >







\#include "Joystick.h"



JoyStick::JoyStick(int x, int y, int z)

:jX(x), jY(y), btn(z){

}



int JoyStick::readX(){

  return jX.read();

}



int JoyStick::readY(){

  return jY.read();

}



void JoyStick::setRangeX(int toMin, int toMax){



  jX.setRange(toMin, toMax);

}



void JoyStick::setRangeY(int toMin, int toMax){

  jY.setRange(toMin, toMax);

}



int JoyStick::readZ(){ // 버튼 상태 읽는 것

  return btn.read();

}



void JoyStick::setCallback(button_callback_t callback){

  btn.setCallback(callback);

}



void JoyStick::check(){

  btn.check();

}



joystick_value_t JoyStick::read(){

  joystick_value_t value;

  value.x = readX();

  value.y = readY();

  value.z = readZ();

  return value;

}






![img](https://blogfiles.pstatic.net/MjAyMDA5MTdfNDEg/MDAxNjAwMzUyOTYzODQ0._4Zeb9yRfo6NocxLIKQvV0Fu7JY5HEavVff_4tzrBh8g.A9KNPnhwGHoX9lVt68eeh6Zer2HXR5s9VcedRw_PQjMg.JPEG.til_t/IMG_1105.jpg) 





![img](https://ssl.pstatic.net/static.se2/static/img/alter_video.png) 